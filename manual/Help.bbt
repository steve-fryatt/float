REM >!Help
REM
REM Copyright 2001-2014, Stephen Fryatt (info@stevefryatt.org.uk)
REM
REM This file is part of Float:
REM
REM   http://www.stevefryatt.org.uk/software/
REM
REM Licensed under the EUPL, Version 1.1 only (the "Licence");
REM You may not use this work except in compliance with the
REM Licence.
REM
REM You may obtain a copy of the Licence at:
REM
REM   http://joinup.ec.europa.eu/software/page/eupl
REM
REM Unless required by applicable law or agreed to in
REM writing, software distributed under the Licence is
REM distributed on an "AS IS" basis, WITHOUT WARRANTIES
REM OR CONDITIONS OF ANY KIND, either express or implied.
REM
REM See the Licence for the specific language governing
REM permissions and limitations under the Licence.

REM Select help file based on available readers and selected territory.
:
ON ERROR PRINT REPORT$+" at line "+STR$(ERL) : END
:
REM Change this direcrtory as required.
:
resource_dir$="<Float$Dir>.Resources"
:
DIM b% 255
:
resource_path$=resource_dir$+"."+FNterritory_name
SYS "XOS_File",5,resource_path$ TO type%
IF type%<2 OR type%>3 THEN resource_path$=resource_dir$+".UK." ELSE resource_path$+="."
:
SYS "XOS_SetVarVal","Float$HelpText",resource_path$+"HelpTX",LEN(resource_path$+"HelpTX"),0,0
:
SYS "XOS_ReadVarVal","Alias$@RunType_3d6",b%,256,0,0 TO ;flags%
IF (flags% AND 1) THEN file$="HelpTX" ELSE file$="HelpSH"
:
OSCLI("%Filer_Run "+resource_path$+file$)
:
END




DEF FNterritory_name
:
LOCAL territory%
:
SYS "Territory_Number" TO territory%
SYS "Territory_NumberToName",territory%,b%,256
=FNzero_string(b%)




DEF FNzero_string(string%)
:
LOCAL string$
:
WHILE ?string%<>0
 string$+=CHR$(?string%)
 string%+=1
ENDWHILE
=string$
